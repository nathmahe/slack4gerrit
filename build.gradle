buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'distribution'
apply plugin: 'groovy'

group = 'com.ullink.slack'
archivesBaseName = 'slack4gerrit'
version = '1.0.0-SNAPSHOT'

jar {
    archiveBaseName = 'slack4gerrit.jar'
    manifest { attributes 'Main-Class': 'com.ullink.slack.review.Connector' }
}

task dist(dependsOn: 'assemble', type: Zip) {
    from('/') {
        include "slack4gerrit.properties"
        into('/')
    }
    from('script') {
        include "*.*"
        into('/')
    }
    from(jar.outputs.files) { into('lib') }
    from(configurations.runtimeClasspath) { into('lib') }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

signing {
    required false
    sign configurations.archives
}

def pomConfig = {
    name 'Slack 4 Gerrit'
    description 'A Slack Bot to share your gerrit review requests'
    url 'https://github.com/Ullink/slack4gerrit'
    licenses {
        license {
            name 'Apache-2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
        }
    }
    developers {
        developer {
            id 'bcorne'
            name 'Benoit Corne'
            email 'benoit.corne@gmail.com'
        }
    }
    contributors {
        contributor {
            name 'CappCorp'
            email 'cappcorp@gmail.com'
            url 'https://github.com/CappCorp'
        }
    }
    scm {
        connection 'scm:git:git@github.com:Ullink/slack4gerrit.git'
        developerConnection 'scm:git:git@github.com:Ullink/slack4gerrit.git'
        url 'https://github.com/Ullink/slack4gerrit'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

distributions {
    main {
        distributionBaseName = 'slack4gerrit'
        contents {
            into('lib') {
                from { configurations.runtimeClasspath }
                from { jar.getOutputs() }
            }
            from { 'script' }
            from { 'slack4gerrit.properties' }
        }
    }
}

dependencies {
    implementation 'com.ullink.slack:simpleslackapi:1.4.0'
    implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.13'
    implementation 'org.slf4j:slf4j-simple:1.7.7'
    implementation 'org.apache.httpcomponents:httpclient:4.5.3'
    implementation 'com.google.guava:guava:18.0'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'org.mapdb:mapdb:1.0.7'
    implementation 'com.google.inject:guice:4.1.0'
    testImplementation 'org.codehaus.groovy:groovy-all:2.4.4'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testImplementation 'org.objenesis:objenesis:2.1'
    testImplementation 'cglib:cglib-nodep:3.2.2'
}
